apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'au.org.dcw.twitter.ingest.RefetchTweets'

task wrapper(type: Wrapper) {
  gradleVersion = '4.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'twitter-refetch-tweets'
    version =  '0.2'
}

repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    compile 'org.twitter4j:twitter4j-core:4.0.6'
    compile 'com.beust:jcommander:1.72'
    compile "com.fasterxml.jackson.core:jackson-core:2.9.1"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.1"
    compile "com.google.guava:guava:23.0"
}

def timestamp() {
    def date = new Date()
    return date.format('yyyyMMddHHmmss')
}

distributions {
    main {
        contents {
            from { 'README.md' }
            from { 'LICENSE' }
            from { 'twitter.properties-template' }
            from { 'proxy.properties-template' }
        }
    }
    timestamped {
        baseName = project.name + '-SNAPSHOT-' + timestamp()
        contents {
            with main.contents
        }
    }
    privileged {
        baseName = project.name + '_with-credentials-SNAPSHOT-' + timestamp()
        contents {
            with main.contents
            // include own twitter credentials
            from { 'twitter.properties' }
        }
    }
}
